buildscript {
    repositories {
        maven { url "http://maven.jumpmind.com/repo" }
        jcenter()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.moowork.gradle:gradle-node-plugin:1.3.1'
        classpath 'co.riiid:gradle-github-plugin:0.4.2'
        classpath 'gradle.plugin.se.bjurr.gitchangelog:git-changelog-gradle-plugin:1.63'
    }
}

apply plugin: 'se.bjurr.gitchangelog.git-changelog-gradle-plugin'
apply from: '../openpos-gradle/src/main/resources/java.gradle'
apply from: '../openpos-gradle/src/main/resources/release.gradle'

idea {
    project {
        ipr {
            //you can tinker with the output *.ipr file before it's written out
            withXml {
                // turn on annotation processing
                def node = it.asNode().children()[0].get('annotationProcessing')[0];
                node.attributes().clear();
                node.appendNode('profile',['default':'true', 'name':'Default', 'enabled':'true'])
            }
        }
    }
}

task jacocoRootReport(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
    dependsOn = subprojects.test
    additionalSourceDirs = files(subprojects.findAll{it.name != 'openpos-client-libs'}.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(subprojects.findAll{it.name != 'openpos-client-libs'}.sourceSets.main.allSource.srcDirs)
    classDirectories =  files(subprojects.findAll{it.name != 'openpos-client-libs'}.sourceSets.main.output)
    executionData = files(subprojects.findAll{it.name != 'openpos-client-libs'}.jacocoTestReport.executionData)
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
    doFirst {
        executionData = files(executionData.findAll {
            it.exists()
        })
    }
}


task generateSourceArtifact(type: Copy) {
    doFirst {
        println "Rebuilding build/openpos-server-core-lib"
        delete 'build/openpos-server-core-lib'
    }
    into('build/openpos-server-core-lib')
    from('../') {
        exclude '**/target', '**/build', '**/bin', '**/src/test/java', 'openpos.code-workspace',
                'openpos-client-seed','openpos-cordova', 'openpos-test-module', 'openpos-cordova-log-plugin',
                '**/node_modules', '**/dist', 'openpos-test-server', 'openpos-assemble',
                'node', 'azure-pipelines*.yml', '**/*.iml', '**/.project', '**/.classpath', '**/.gitignore', 
                '**/.idea', '**/.settings', '**/.gradle'
    }
    from('../') {
        exclude '**/target', '**/build', '**/bin', '**/src/test/java', 'openpos.code-workspace',
                'openpos-client-seed','openpos-cordova', 'openpos-test-module', 'openpos-cordova-log-plugin',
                'openpos-client-libs', 'openpos-test-server', 'openpos-assemble',
                'node', 'azure-pipelines*.yml', '**/*.iml', '**/.project', '**/.classpath', '**/.gitignore',
                '**/.idea', '**/.settings', '**/.gradle'
        include '**/openpos-version.properties'
                filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [
                    'version': version,
                    'buildTime': new java.text.SimpleDateFormat('yyyy-MM-dd HH:mm:ss').format(new java.util.Date()),
                    'gitBranch' : gitBranch,
                    'gitHash' : gitHash,
                    'buildNumber' : buildNumber,
                    'buildName' : buildName
                ])
    }
}

task generateChangeLog(type: Class.forName('se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask')) {
    gitHubApi = gitHubApi
    gitHubToken = personalAccessToken
    fromRef = '0.3.6'
    file = new File("../CHANGELOG.md");
    fromRepo = "$projectDir/.."
    templateContent = """# Change Log
{{#tags}}

## {{name}} 
{{#hasTagTime}}
Released on: {{tagTime}}
{{/hasTagTime}}

|       | Name   | Labels | Author | Time |
|-------|--------|--------|--------|------|
 {{#issues}}
  {{#hasLink}}  
| [{{issue}}]({{link}}) | {{title}} | {{#hasLabels}}{{labels}}{{/hasLabels}} |{{#commits}}{{authorName}}|{{commitTime}}{{/commits}}|
  {{/hasLink}}
 {{/issues}}
{{/tags}}
""";
}
