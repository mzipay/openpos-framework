buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath "se.bjurr.gitchangelog:git-changelog-gradle-plugin:1.63"
    }
}

apply plugin: 'se.bjurr.gitchangelog.git-changelog-gradle-plugin'
apply plugin: 'com.moowork.node'
apply plugin: 'co.riiid.gradle'

def gitCommit = { files ->

    files.each {
        def file = it
        exec {
            commandLine 'git', 'add', file
        }
    }

    exec {
        commandLine 'git', 'config', 'user.email', 'build@jumpmind.com'
    }
    exec {
        commandLine 'git', 'config', 'user.name', 'build'
    }
    exec {
        commandLine 'git', 'config', 'remote.origin.url', remoteRepoUrl.replace('${personalAccessToken}', personalAccessToken)
    }

    exec {
        commandLine 'git', 'commit', '-m', "releasing ${version}"
        ignoreExitValue true
    }
    exec {
        commandLine 'git', 'push', 'origin', 'HEAD:' + gitBranch
        ignoreExitValue true
    }
}


def updateGradlePropertiesVersion = { newVersion ->
    def propertyFile = file 'gradle.properties'
    def props = new Properties()
    propertyFile.withInputStream { props.load(it) }
    props['version'] = newVersion
    propertyFile.withWriter { props.store(it, null) }
}


node {
    version = '10.5.0'
    npmVersion = '6.1.0'
    download = downloadNode.toBoolean()
    println """
      **************************************************************************************************
        Using native node & npm? ${!download}.  Set downloadNode to true in gradle.properties 
        if you want to use downloaded versions of node and npm
      **************************************************************************************************
      """
    workDir = file("${projectDir}/node")
    nodeModulesDir = file("./")
}

github {
    owner = repoOwner
    repo = repoName
    token = personalAccessToken
    tagName = version
    targetCommitish = gitBranch
    name = version
    prerelease = false
}

task updateVersionAndGradleProperties() {
    doLast {
        updateGradlePropertiesVersion(version)
    }
}

task setPackageJsonVersion(type: NpmTask) {
    dependsOn updateVersionAndGradleProperties
    workingDir = file(packageJsonDir)
    args = [ 'version', '--allow-same-version', version ]
}


task prereleaseCommit() {
    dependsOn setPackageJsonVersion
    doLast {
        gitCommit ([ 'gradle.properties', packageJsonDir + '/package.json', packageJsonDir + '/package-lock.json' ])
    }
}

task prerelease() {
    if (releaseBuildEnabled.toBoolean()) {
        dependsOn prereleaseCommit
        doLast {
            println "prerelease ${version}"
        }
    }
}

task npm_publish(type: NpmTask) {
    dependsOn githubRelease
    args = [
            'publish',
            "${projectDir}/../openpos-client-libs/dist/openpos-client-core-lib"
    ]
}

// had to declare this a second time because tasks can only execute once
// calling this manually from the release task
task setPackageJsonVersionToSnapshot(type: NpmTask) {
    dependsOn updateVersionAndGradleProperties
    workingDir = file(packageJsonDir)
    args = [ 'version', '--allow-same-version', version ]
}

task gitChangelogTask(type: Class.forName('se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask')) {
    dependsOn npm_publish
    gitHubApi = gitHubApi
    gitHubToken = personalAccessToken
    file = new File("../CHANGELOG.md");
    fromRef = '0.3.4'
    toRef = version
    templateContent = """
# Release Notes
{{#tags}}

## Release {{name}} 
Release time: {{tagTime}}
 {{#issues}}
  {{#hasLink}}
#### [{{issue}}]({{link}}) {{title}}
  {{#hasLabels}}
{{labels}}
  {{/hasLabels}}
  {{#commits}}
by {{authorName}} 
  {{/commits}}
  {{/hasLink}}
 {{/issues}}
{{/tags}}
""";
}

task release() {
    dependsOn gitChangelogTask
    doFirst {
        if (!releaseBuildEnabled.toBoolean()) {
            throw new GradleException('You need to specify -PreleaseBuildEnabled=true when running the release task')
        }
    }
    doLast {
        def versionParts = version.split('\\.')
        def lastPart = versionParts[versionParts.length-1] as int
        lastPart++
        version = ''
        for(int i=0; i < versionParts.length-1; i++) {
            version = version + versionParts[i] + '.'
        }
        version = version + lastPart + '-SNAPSHOT'
        updateGradlePropertiesVersion(version)
        tasks.setPackageJsonVersionToSnapshot.args[2] = version
        tasks.setPackageJsonVersionToSnapshot.execute()
        gitCommit (['gradle.properties', packageJsonDir + '/package.json', packageJsonDir + '/package-lock.json', '../CHANGELOG.md'])
    }
}

subprojects.each { tasks.getByPath(":${it.name}:processResources").dependsOn prerelease }
subprojects.each { tasks.githubRelease.dependsOn("${it.name}:deploy") }
tasks.gitChangelogTask.dependsOn githubRelease
tasks.githubRelease.dependsOn prerelease
