apply plugin: 'com.moowork.node'
apply from: '../openpos-gradle/src/main/resources/java.gradle'

repositories {
    mavenCentral()
}

configurations {
    sshAntTask
}
dependencies {
    sshAntTask 'org.apache.ant:ant-jsch:1.10.5'
    sshAntTask 'com.jcraft:jsch:0.1.55'
}

node {
    version = '10.5.0'
    npmVersion = '6.1.0'
    download = downloadNode.toBoolean()
    println """
      **************************************************************************************************
        Using native node & npm? ${!download}.  Set downloadNode to true in gradle.properties 
        if you want to use downloaded versions of node and npm
      **************************************************************************************************
      """
    workDir = file("${projectDir}/node")
    nodeModulesDir = file("./")
}

task npm_install(type: NpmTask){
    args = [
            'install'
    ]
    inputs.files fileTree('package.json')
    outputs.dir file('node_modules')
}

task ng_test(type: NpmTask){
    dependsOn npm_install
    args = [
            'run', 
            'test'
    ]
    inputs.files fileTree('projects/openpos-client-core-lib')
}

task ng_build(type: NpmTask){
    dependsOn npm_install
    args = [
            'run',
            'build'
    ]
    inputs.files fileTree('projects/openpos-client-core-lib/**/*')
    outputs.dir file('dist/openpos-client-core-lib')
}

task ng_clean {
    delete ng_build.outputs
    delete npm_install.outputs
}

task npm_pack(type: NpmTask){
    dependsOn ng_build
    workingDir =  file('dist/openpos-client-core-lib')
    args = [
            'pack'
    ]
}

task ng_deploy {
    dependsOn npm_pack
    doLast {
        if (!gitBranch.equals('merge')) {
            ant.taskdef(name: 'scp', classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp', classpath: configurations.sshAntTask.asPath)
            def toDir = "$deployUser:$deployPassword@maven.jumpmind.com:~/www/npm"

            fileTree('dist/openpos-client-core-lib').visit { FileVisitDetails details -> 
                if (details.file.path.endsWith('tgz')) {
                    ant.scp(todir: toDir, port: 2222, trust: 'true', verbose: 'true', localFile: details.file.path)          
                    def snapshotFile = 'dist/openpos-client-core-lib/jumpmind-openpos-client-core-lib-snapshot.tgz'
                    ant.copy(file: details.file.path, tofile: snapshotFile)
                    ant.scp(todir: toDir, port: 2222, trust: 'true', verbose: 'true', localFile: snapshotFile)
                }
            }
        } else {
            println('Not going to deploy this build because the gitBranch indicates that this is a pull request')
        }
    }
}

deploy.dependsOn ng_deploy
test.dependsOn ng_test
build.dependsOn ng_build
clean.dependsOn ng_clean
